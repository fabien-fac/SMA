import classes.Position
import interfaces.IActionsAgent
import interfaces.ICreateAgent
import interfaces.ICreateAgentAvecProxy
import interfaces.ICreateProxyAgent
import interfaces.IGetAgent
import interfaces.IInfos
import interfaces.ILog
import interfaces.IPercevoir
import interfaces.IControl
import interfaces.IInfosAgents
import interfaces.IInfosGrille

namespace SMA {

	ecosystem Agents {

		provides createElement: ICreateAgent
		provides getElement: IGetAgent

		species Agent(nom: String, position: Position, couleur: String) {
			provides infosAgent: IInfos
			provides actionsAgent: IActionsAgent
			requires percevoirAgent: IPercevoir
		}
	}
	
	ecosystem ProxysAgent {
		
		provides createProxyAgent: ICreateProxyAgent
		requires percevoirProxy: IPercevoir
		
		species ProxyAgent(nomAgent: String){
			provides percevoirProxySpecy: IPercevoir
		}
	}
	
	ecosystem AgentsAvecProxyEcosystem {
		
		provides infosSurAgents: IInfosAgents
		provides actionsAgentsProxy: IActionsAgent
		provides createAgentAvecProxy: ICreateAgentAvecProxy
		requires percevoirAP: IPercevoir
		
		part proxys: ProxysAgent{
			bind percevoirProxy to percevoirAP
		}
		
		part agents: Agents
		
		species AgentAvecProxy(nom: String, position: Position, couleur: String){
			use proxy: proxys.ProxyAgent(nom)
			use agent: agents.Agent(nom, position, couleur){
				bind percevoirAgent to proxy.percevoirProxySpecy
			}
		}
	}

	component Logger {
		provides log: ILog
	}
	
	component System {
		provides perceptionSystem : IPercevoir
		provides control: IControl
		provides infosGrille: IInfosGrille
		requires createAgents: ICreateAgentAvecProxy
		requires actionsSurAgents: IActionsAgent
	}
	
	component IHM {
		requires infosGrille: IInfosGrille
	}
	
	component BigComponent {
		provides control: IControl
		
		part system: System{
			bind createAgents to agents.createAgentAvecProxy
			bind actionsSurAgents to agents.actionsAgentsProxy
		}
		
		part agents : AgentsAvecProxyEcosystem{
			bind percevoirAP to system.perceptionSystem
		}
		
		part ihm : IHM{
			bind infosGrille to system.infosGrille
		}
		
	}
	

}